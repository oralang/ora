// Test file for formal verification annotations
// This demonstrates requires, ensures, invariants, ghost functions, and ghost variables

contract FVTest {
    error InsufficientBalance;

    storage balance: u256 = 0;
    storage max_balance: u256 = 1000;
    
    // Ghost variable for verification (not in bytecode)
    ghost storage sumOfBalances: u256 = 0;

    pub fn transfer(amount: u256, to: address) -> !bool
        requires(amount > 0)
        requires(amount <= balance)
        requires(to != std.constants.ZERO_ADDRESS)
        ensures(balance <= old(balance))
    {
        balance = balance - amount;
        return true;
    }

    pub fn deposit(amount: u256) -> !u256
        requires(amount > 0)
        requires(balance <= max_balance)
        requires(amount <= max_balance - balance)
        ensures(balance >= old(balance))
        ensures(balance <= max_balance)
        ensures(sumOfBalances == old(sumOfBalances))
    {
        balance = balance + amount;
        return balance;
    }
    
    // Ghost function for verification (not in bytecode)
    ghost fn getTotalBalance() -> u256 {
        let total: struct { value: u256, overflow: bool } = @addWithOverflow(balance, sumOfBalances);
        if (total.overflow) {
            return std.constants.U256_MAX;
        }
        return total.value;
    }

    pub fn withdraw(amount: u256) -> !u256
        requires(amount > 0)
        requires(amount <= balance)
        ensures(balance <= old(balance))
    {
        balance = balance - amount;
        return balance;
    }

    pub fn loop_example(n: u256) -> !u256
        requires(n <= 100)
    {
        var sum: u256 = 0;
        var i: u256 = 1;
        
        while (i <= n)
            invariant(i >= 1)
            invariant(i <= n + 1)
            invariant(sum <= 5050)
            decreases(n + 1 - i)
        {
            sum = sum + i;
            i = i + 1;
        }
        
        return sum;
    }

    pub fn assume_example(x: u256) -> !u256
        requires(x > 0)
    {
        assume(x < 100);
        return x * 2;
    }

    pub fn havoc_example() -> !u256 {
        var y: u256 = 10;
        havoc y;
        return y;
    }
}
